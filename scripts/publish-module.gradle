apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android SDK помогает реализовать получение кода авторизации (Auth Code) Сбер ID минимальными усилиями со стороны разработчика и в соответствии с утвержденными гайдами по отображению кнопки.'
                    url = 'https://github.com/SberID/android-sdk'
                    licenses {
                        license {
                            name = 'Sber Public License at-nc-sa v.2'
                            url = 'https://github.com/SberID/android-sdk/blob/master/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'JeN1ks'
                            name = 'Nikita Gerasimenko'
                            email = 'gerasimenko.n.serg@sberbank.ru'
                        }
                        developer {
                            id = 'AleksandrLelyukh'
                            name = 'Aleksandr Lelyukh'
                            email = 'AALelyuh@sberbank.ru'
                        }
                    }

                    scm {
                        connection = 'scm:github.com/SberID/android-sdk/tree/master/SberIdSDK.git'
                        developerConnection = 'scm:git:ssh://github.com/SberID/android-sdk/tree/master/SberIdSDK.git'
                        url = 'https://github.com/SberID/android-sdk/tree/master/SberIdSDK'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}