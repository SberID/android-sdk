def getNexusCredentials() {
    Properties properties = new Properties()
    def propertiesFile = file("$rootProject.projectDir/local.properties")
    if (propertiesFile.exists()) {
        propertiesFile.withInputStream {  stream ->
            properties.load(stream)
        }
    }

    def user = properties.getProperty('nexus.user')
    def password = properties.getProperty('nexus.password')
    throwErrorIfNull(user, 'nexus.user')
    throwErrorIfNull(password, 'nexus.password')
    return [
            user    : user,
            password: password,
    ]
}

static def throwErrorIfNull(String value, String key) {
    if (value == null) {
        throw new IllegalStateException(
                """\
                Не найдены данные УЗ для доступа в Nexus, задайте $key в local.properties.
                """.stripIndent()
        )
    }
}

ext {
    getNexusCredentials = this.&getNexusCredentials
}
